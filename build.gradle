buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        google()
        jcenter()
        maven {
            name "gradle-plugins"
            url "https://plugins.gradle.org/m2/"
        }
    }

    ext.kotlin_version = '1.2.20'

    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        //noinspection DifferentKotlinGradleVersion
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'pl.allegro.tech.build:axion-release-plugin:1.9.0'
    }
}

apply plugin: 'pl.allegro.tech.build.axion-release'
apply plugin: 'maven'


scmVersion {
    tag {
        prefix = ''
    }

    versionCreator 'versionWithBranch'
    hooks {
        pre 'fileUpdate', [file: 'README.md', pattern: { previous, context -> />$previous<\// }, replacement: { current, context -> ">$current</" }]
        pre 'fileUpdate', [file: 'README.md', pattern: { previous, context -> /:$previous'/ }, replacement: { current, context -> ":$current\'" }]
        pre 'commit', { v, p -> "Automatic update for release $v" }
        post 'push'
    }
}

allprojects {
    buildscript {
        repositories {
            mavenLocal()
            mavenCentral()
            google()
            jcenter()
            maven {
                name "gradle-plugins"
                url "https://plugins.gradle.org/m2/"
            }
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
        google()
        jcenter()
        maven { url "https://jitpack.io" }
        // For Ice pick 3.*
        maven { url "https://clojars.org/repo/" }
    }

    project.version = scmVersion.version
    project.group = rootProject.group

    ext.calculateVersionCode = { String versionName ->
        def shortVersion = versionName.split("-", 2)[0]
        def strings = shortVersion.split("\\.", 3)
        def numbers = strings.collect {
            try {
                it.toInteger()
            } catch (ignored) {
                0
            }
        }
        while (numbers.size < 3) {
            numbers.add(0)
        }
        def result = 0
        numbers.eachWithIndex { number, i ->
            if (i < 3) {
                result = result * 1000 + number
            }
        }
        result
    }

}

/*
 * Copyright 2018, Chiswick Forest
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and limitations under the License.
 */

// Define dependency versions in a single place
ext {
    // Sdk and tools
    // Support library and architecture components support minSdk 14 and above.
    minSdkVersion = 21
    targetSdkVersion = 27
    compileSdkVersion = 27

    // App dependencies
    supportLibraryVersion = '27.1.0'
    constraintLayoutVersion = '1.0.2'
    junitVersion = '4.12'
    mockitoVersion = '2.6.1'
    mockitoKotlinVersion = '2.0.0-alpha03'
    powerMockito = '1.6.2'
    hamcrestVersion = '1.3'
    runnerVersion = '1.0.1'
    rulesVersion = '1.0.1'
    espressoVersion = '3.0.1'

    streamVersion = '1.1.9'
    autoValueVersion = '1.5.1'
    rxJava2Version = '2.1.10'
    rxAndroidVersion = '2.0.2'
    rxKotlinVersion = '2.2.0'
    horrockVersion= '0.2.2'
    simpleXmlVersion = '2.7.1'
    daggerVersion = '2.14.1'
    utilsVersion = '1.2.3'
    activityStarterVersion = '1.11-beta'
    butterknifeVersion = '8.8.1'
    dexmakerVersion = '1.2'
}

